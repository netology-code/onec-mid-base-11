
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	//{{ Изменение конфигурации м56
	м62_ДобавитьЭлементСогласованнаяСкидка();
	//}}
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
    ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	м62_ПересчитатьЗаказ();
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РассчитатьСуммуСтроки(ТекущиеДанные)
	
	// {{ Доработка а56 - учесть скидку
	// КоэффициентСкидки = 1 - ТекущиеДанные.Скидка / 100;
	// ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество;
	м62_ПересчитатьСтроку(ТекущиеДанные);
    // }}
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДокумента()

	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма") + Объект.Услуги.Итог("Сумма");
	
КонецПроцедуры

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаСервере
Процедура м62_ДобавитьЭлементСогласованнаяСкидка()
    ГруппаКолонкиШапкиЕлемент = Элементы.ГруппаКолонкиШапки;
	ГруппаСкидкаЭлемент = Элементы.Вставить("м62_ГруппаСкидка", Тип("ГруппаФормы"),ГруппаКолонкиШапкиЕлемент.Родитель, ГруппаКолонкиШапкиЕлемент);
	Элементы.Переместить(ГруппаКолонкиШапкиЕлемент,ГруппаКолонкиШапкиЕлемент.Родитель, ГруппаСкидкаЭлемент); 
	ГруппаСкидкаЭлемент.Вид = ВидГруппыФормы.ОбычнаяГруппа;

	СкидкаЭлемент = Элементы.Добавить("м62_СогласованнаяСкидка", Тип("ПолеФормы"),ГруппаСкидкаЭлемент);
	СкидкаЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	СкидкаЭлемент.ПутьКДанным = "Объект.м62_СогласованнаяСкидка";
	СкидкаЭлемент.УстановитьДействие("ПриИзменении", "м62_СогласованнаяСкидкаПриИзменении"); 

	Команда = Команды.Добавить("м62_Пересчитать");
	Команда.Заголовок = "Пересчитать";
	Команда.Действие = "м62_КомандаПересчитать";

	КнопкаЭлемент = Элементы.Добавить("м62_Пересчитать", Тип("КнопкаФормы"),ГруппаСкидкаЭлемент);
	КнопкаЭлемент.ИмяКоманды = "м62_Пересчитать";  
	КнопкаЭлемент.Вид = ВидКнопкиФормы.ОбычнаяКнопка;	
КонецПроцедуры                                        

//@skip-check module-unused-method
&НаКлиенте
Процедура м62_КомандаПересчитать(Элемент)
	м62_ПересчитатьЗаказ();      
КонецПроцедуры                                        

//@skip-check module-unused-method
&НаКлиенте
Процедура м62_СогласованнаяСкидкаПриИзменении(Элемент)
	Если Объект.Товары.Количество() > 0 Или Объект.Услуги.Количество() > 0 Тогда
		ПоказатьВопрос(
				Новый ОписаниеОповещения("м62_ПослеЗакрытияВопросаОПересчете",ЭтотОбъект)
			,	"Пересчитать документ?"
			,	РежимДиалогаВопрос.ДаНет
			,
			,	КодВозвратаДиалога.Да
			,	"Изменен параметр ""Согласованная скидка"""
		);
	КонецЕсли;
КонецПроцедуры // м62_СогласованнаяСкидкаПриИзменении()

&НаКлиенте
Процедура м62_ПослеЗакрытияВопросаОПересчете(Результат, Параметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		м62_ПересчитатьЗаказ();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура м62_ПересчитатьЗаказ()
	Для каждого Строка из Объект.Товары Цикл
		м62_ПересчитатьСтроку(Строка);
	КонецЦикла;
	Для каждого Строка из Объект.Услуги Цикл
		м62_ПересчитатьСтроку(Строка);
	КонецЦикла;
	РассчитатьСуммуДокумента();    
КонецПроцедуры

&НаКлиенте
Процедура м62_ПересчитатьСтроку(Строка)
	Строка.Сумма 	= Строка.Цена 
					* Строка.Количество 
					* (1 - Строка.Скидка / 100)
					* (1 - Объект.м62_СогласованнаяСкидка / 100);
КонецПроцедуры

#КонецОбласти

#КонецОбласти
