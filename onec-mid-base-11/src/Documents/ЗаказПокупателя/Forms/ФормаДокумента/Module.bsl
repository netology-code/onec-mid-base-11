
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	//{+ ОбвинцевДС 25.12.2024
	//Добавление поля "Контактое лицо"
    ГруппаШапкаПраво = Элементы.ГруппаШапкаПраво;
	ПолеВвода = Элементы.Добавить("ЭлементКонтактноеЛицо",Тип("ПолеФормы"), ГруппаШапкаПраво); 
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ОД_КонтактноеЛицо";	
	
	// Добавление поля "Согласованная скидка" 
	ГруппаШапкаЛево = Элементы.Добавить("ОД_ПрименениеСкидки",Тип("ГруппаФормы"),Элементы.ГруппаШапкаЛево);
	ГруппаШапкаЛево.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаШапкаЛево.Заголовок = "Группа полей для ведения скидки";
    ПолеВвода = Элементы.Добавить("ЭлементСогласованнаяСкидка",Тип("ПолеФормы"), ГруппаШапкаЛево); 
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ОД_СогласованнаяСкидка"; 
	ПолеВвода.УстановитьДействие("ПриИзменении","ОД_ПриИзмененииСкидки");
	
	// Добавление новой команды "Применить скидку" по пересчету суммы позиций в таблицах с учетом скидки
    КомандаПрименитьСкидку = Команды.Добавить("ОД_ПрименитьСкидку");
	КомандаПрименитьСкидку.Заголовок = "ЗаполнитьСкидку";
	КомандаПрименитьСкидку.Действие = "ОД_КомандаПрименитьСкидку"; 
	КнопкаФормыПрименитьСкидку = Элементы.Добавить("ОД_КнопкаПрименитьСкидку",Тип("КнопкаФормы"),ГруппаШапкаЛево);
	КнопкаФормыПрименитьСкидку.ИмяКоманды = "ОД_ПрименитьСкидку";
	КнопкаФормыПрименитьСкидку.Вид = ВидКнопкиФормы.ОбычнаяКнопка;	
	//+} ОбвинцевДС 25.12.2024
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
    ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РассчитатьСуммуСтроки(ТекущиеДанные)
	
	КоэффициентСкидки = 1 - ТекущиеДанные.Скидка / 100;
	ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество * КоэффициентСкидки;
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДокумента()
	
	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма") + Объект.Услуги.Итог("Сумма");
	
КонецПроцедуры

//{+ ОбвинцевДС 25.12.2024
// Добавление обработки новой команды и изменения поля "Согласованная скидка"
&НаКлиенте 
Процедура ОД_КомандаПрименитьСкидку(Команда)
 
	ЗаполнитьСкидкуПозицийТоваровУслуг();
	
 КонецПроцедуры

// Добавление обработки изменения поля "Согласованная скидка"

&НаКлиенте
Процедура ОД_ПриИзмененииСкидки(Элемент)
 
	Режим = РежимДиалогаВопрос.ДаНет;
	Оповещение = Новый ОписаниеОповещения ("ЗаполнитьСкидку", ЭтотОбъект, Параметры);
	
    ПоказатьВопрос(Оповещение, "Процент скидки изменен. Заполнить данные в табличной части Товары и Услуги?", Режим, 0);	
 
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСкидку(Результат, Параметры) Экспорт

	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСкидкуПозицийТоваровУслуг();

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСкидкуПозицийТоваровУслуг()
	
		ТаблицаТоваров = Объект.Товары;
		Для каждого Товар Из ТаблицаТоваров Цикл
		
			Товар.Скидка =  Объект.ОД_СогласованнаяСкидка;
			РассчитатьСуммуСтроки(Товар);
		КонецЦикла;
	
		ТаблицаУслуги = Объект.Услуги;
		Для каждого Услуга Из ТаблицаУслуги Цикл
		
			Услуга.Скидка =  Объект.ОД_СогласованнаяСкидка;
			РассчитатьСуммуСтроки(Услуга);
		КонецЦикла;
	РассчитатьСуммуДокумента();

КонецПроцедуры
//+} ОбвинцевДС 25.12.2024

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти
