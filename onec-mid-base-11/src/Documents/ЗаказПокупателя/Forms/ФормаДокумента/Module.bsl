
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
    ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
КонецПроцедуры

#КонецОбласти

// +++++ Павленко А.В. добавлена Область с Обработчиком
#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Асинх Процедура СогласованнаяСкидкаПриИзмененииПосле(Элемент)
    
    ТЧТовары = Объект.Товары;
    ТЧУслуги = Объект.Услуги;
    
    Если ТЧТовары.Количество() > 0 Или ТЧУслуги.Количество() > 0 Тогда
        
        Обещание = ВопросАсинх("Изменен процент скидки. Пересчитать таблицы Товаров и Услуг?", РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
        Результат = Ждать Обещание;
        
        Если Результат = КодВозвратаДиалога.Да Тогда
            
            ПересчетТаблицТоварыИУслуги(ТЧТовары, ТЧУслуги);
            
        КонецЕсли;
        
    КонецЕсли;
    
КонецПроцедуры


#КонецОбласти
// +++++

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

#КонецОбласти

// +++++ Павленко А.В. добавлена Область с Обработчиком
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПересчитатьТаблицуПосле(Команда)
    
    ТЧТовары = Объект.Товары;
    ТЧУслуги = Объект.Услуги;
    
    ПересчетТаблицТоварыИУслуги(ТЧТовары, ТЧУслуги);
    
КонецПроцедуры

#КонецОбласти
// +++++

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РассчитатьСуммуСтроки(ТекущиеДанные)
	
	КоэффициентСкидки = 1 - ТекущиеДанные.Скидка / 100;
	ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество * КоэффициентСкидки;
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДокумента()
	
	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма") + Объект.Услуги.Итог("Сумма");
	
КонецПроцедуры

// +++++ Павленко А.В. добавлен пересчет строк ТЧ, с учетом согласованной скидки
&НаКлиенте
Процедура ПересчетТаблицТоварыИУслуги(ТЧТовары, ТЧУслуги)
    
    Для Каждого СтрокаТЧ Из ТЧТовары Цикл
    	СтрокаТЧ.Скидка = Объект.СогласованнаяСкидка;
        СтрокаТЧ.Сумма = (СтрокаТЧ.Цена * СтрокаТЧ.Количество)* (1 - Объект.СогласованнаяСкидка / 100);
    КонецЦикла;
    
    Для Каждого СтрокаТЧ Из ТЧУслуги Цикл
    	СтрокаТЧ.Скидка = Объект.СогласованнаяСкидка;
        СтрокаТЧ.Сумма = (СтрокаТЧ.Цена * СтрокаТЧ.Количество)* (1 - Объект.СогласованнаяСкидка / 100);
    КонецЦикла;
     
КонецПроцедуры
// +++++

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти
