
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	//++ Арсеньева ДЗ ВнесениеИзмененийВКонфигурацию 13,06.25
	//Добавляем на форму реквизит ТП_КонтактноеЛицо
	Если Элементы.Найти("ТП_КонтактноеЛицо") = Неопределено Тогда
		 
		 ЭлементКонтактноеЛицо  = Элементы.Добавить("ТП_КонтактноеЛицо", Тип ("ПолеФормы"),Элементы.ГруппаШапкаПраво);
		 ЭлементКонтактноеЛицо.ПутьКДанным = "Объект.ТП_КонтактноеЛицо";
		 ЭлементКонтактноеЛицо.Вид = ВидПоляФормы.ПолеВвода;
		 ЭлементКонтактноеЛицо.Заголовок = "Контактное лицо (ТП)";
		 
	 КонецЕсли; 
	 // --
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
    
    //++ Арсеньева ДЗ ВнесениеИзмененийВКонфигурацию 13.04.25
    
    Если Элементы.Найти("ТП_Скидка") = Неопределено Тогда
		//Добавление ГруппуСкидки, в которую войдут поле Скидка и кнопка Пересчитать
		ЭлементГруппаСкидки = Элементы.Добавить("ГруппаСкидки", Тип("ГруппаФормы"), Элементы.ГруппаШапкаЛево);
		ЭлементГруппаСкидки.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		ЭлементГруппаСкидки.Отображение = ОтображениеОбычнойГруппы.Нет;
		ЭлементГруппаСкидки.ОтображатьЗаголовок = Ложь;
		ЭлементГруппаСкидки.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
		
		//Добавление поля ввода Скидка	
		ЭлементСкидка = Элементы.Добавить("ТП_Скидка", Тип("ПолеФормы"), Элементы.ГруппаСкидки);
		ЭлементСкидка.ПутьКДанным = "Объект.ТП_Скидка";
		ЭлементСкидка.Вид = ВидПоляФормы.ПолеВвода;
		ЭлементСкидка.Заголовок = "Скидка (ТП)";
		ЭлементСкидка.УстановитьДействие("ПриИзменении", "ТП_СкидкаПриИзменении"); 
	КонецЕсли;
	
	Если Элементы.Найти("ТП_ПересчитатьТаблицу") = Неопределено Тогда
		//Добавление кнопки Пересчитать
		КомандаПересчитать = Команды.Добавить("ТП_ПересчитатьТаблицу");
		КомандаПересчитать.Заголовок = "ПересчитатьТаблицу (ТП)";
		КомандаПересчитать.Действие = "ТП_ПересчитатьТаблицу";
		
		КнопкаПересчитать = Элементы.Вставить("ТП_КнопкаПересчитать", Тип("КнопкаФормы"), Элементы.ГруппаСкидки);
		КнопкаПересчитать.ИмяКоманды = "ТП_ПересчитатьТаблицу";
		КнопкаПересчитать.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
		КнопкаПересчитать.Отображение  = ОтображениеКнопки.КартинкаИТекст;
		КнопкаПересчитать.Заголовок = "Пересчитать таблицу (ТП)";
		КнопкаПересчитать.Картинка = БиблиотекаКартинок.Перечитать;
		
	КонецЕсли;
    
    //--
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
    ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РассчитатьСуммуСтроки(ТекущиеДанные)
	
	//++ Арсеньева ДЗ ВнесениеИзмененийВКонфигурацию 13.04.25, расчитываем Сумму, с учетом скидки
	
	ОбщийКоофСкидки = (Объект.ТП_Скидка + ТекущиеДанные.Скидка) / 100;
	Если ОбщийКоофСкидки > 1 Тогда
		Сообщить ("Скидка превышает 100%");
		ТекущиеДанные.Сумма = 0;
	Иначе
	ТекущиеДанные.Сумма = (ТекущиеДанные.Цена * ТекущиеДанные.Количество) - 
	(ТекущиеДанные.Цена * ТекущиеДанные.Количество) * ОбщийКоофСкидки;
	КонецЕсли;

	РассчитатьСуммуДокумента();
	// --
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДокумента()
	
	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма") + Объект.Услуги.Итог("Сумма");
	
КонецПроцедуры

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

&НаКлиенте
Процедура ПересчитатьТаблицуСУчетомСкидки()
	//++ Арсеньева ДЗ ВнесениеИзмененийВКонфигурацию 13.06.25	
	Для Каждого Строка Из Объект.Товары Цикл
		Если Строка = Неопределено Тогда
			Возврат;
		Иначе			
			РассчитатьСуммуСтроки(Строка);
		КонецЕсли;		
	КонецЦикла; 
	
	Для Каждого Строка Из Объект.Услуги Цикл
		Если Строка = Неопределено Тогда
			Возврат;
		Иначе			
			РассчитатьСуммуСтроки(Строка);
		КонецЕсли;
	КонецЦикла; 
	// --	
КонецПроцедуры

#КонецОбласти
#Область ОбработчикиСобытийДобавленныхЭлементов

&НаКлиенте
Процедура ТП_ПересчитатьТаблицу(Команда)
	//++ Арсеньева ДЗ ВнесениеИзмененийВКонфигурацию 13.06.25
	ПересчитатьТаблицуСУчетомСкидки();
    // --
КонецПроцедуры

&НаКлиенте
Процедура ТП_СкидкаПриИзменении(Элемент)
	//++ Арсеньева ДЗ ВнесениеИзмененийВКонфигурацию 13.06.25
	ЗадатьВопросОСкидкеАсинх();	
	// --
КонецПроцедуры 

&НаКлиенте
Асинх Процедура ЗадатьВопросОСкидкеАсинх()
	//++ Арсеньева ДЗ ВнесениеИзмененийВКонфигурацию 13.04.25
	Обещание = ВопросАсинх("Изменен процент скидки. Пересчитать таблицы товары и услуги?", РежимДиалогаВопрос.ДаНет);
	ОтветНаВопрос = Ждать Обещание;
		
	Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
		
		ПересчитатьТаблицуСУчетомСкидки();	
		
	Иначе
		Возврат;
	КонецЕсли;
	// --
КонецПроцедуры


#КонецОбласти

