
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	//++Грунтова Задание№13 17.09.2024
	//Вывести на форму Контактное Лицо
	Если Элементы.Найти("КонтактноеЛицо") = Неопределено Тогда
	    ДобавляемыеРеквизиты = Новый Массив;
		
		ТипРеквизита = Новый ОписаниеТипов("СправочникСсылка.КЛ_КонтактныеЛица");
		РеквизитКонтактноеЛицо = Новый РеквизитФормы("КонтактноеЛицо", ТипРеквизита, "", "КонтактноеЛицо");
		
		ДобавляемыеРеквизиты.Добавить(РеквизитКонтактноеЛицо);
		
		ИзменитьРеквизиты(ДобавляемыеРеквизиты);
		
		НовыйЭлемент = Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Элементы.ГруппаШапкаПраво);
		НовыйЭлемент.ПутьКДанным = "Объект.КЛ_КонтактноеЛицо";
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	КонецЕсли; 
    //--Грунтова Задание№13 17.09.2024
    
    //++Грунтова Задание№13 17.09.2024
	//Вывести на форму СогласованнаяСкидка
	ГруппаСогласованнаяСкидка = ЭтотОбъект.ЭтаФорма.Элементы.Добавить("ГруппаОбычная", Тип("ГруппаФормы"), Элементы.ГруппаШапкаЛево);
	ГруппаСогласованнаяСкидка.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаСогласованнаяСкидка.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаСогласованнаяСкидка.ОтображатьЗаголовок = ЛОЖЬ; 
	ГруппаСогласованнаяСкидка.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	
	Если Элементы.Найти("СогласованнаяСкидка") = Неопределено Тогда 
		
		ДобавляемыеРеквизиты = Новый Массив;
		
		ТипРеквизита = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(3,0));
		
		РеквизитСогласованнаяСкидка = Новый РеквизитФормы("СогласованнаяСкидка", ТипРеквизита, "", "СогласованнаяСкидка");
		ДобавляемыеРеквизиты.Добавить(РеквизитСогласованнаяСкидка);
		
		ИзменитьРеквизиты(ДобавляемыеРеквизиты);
		
		ЭлементСогласованнаяСкидка = Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"), ГруппаСогласованнаяСкидка);
		ЭлементСогласованнаяСкидка.ПутьКДанным = "Объект.КЛ_СогласованнаяСкидка";
		ЭлементСогласованнаяСкидка.Вид = ВидПоляФормы.ПолеВвода;
		ЭлементСогласованнаяСкидка.УстановитьДействие("ПриИзменении", "ВыполнитьДействиеСогласованнаяСкидкаПриИзменении");
	КонецЕсли;
    //--Грунтова Задание№13 17.09.2024

	//++Грунтова Задание№13 17.09.2024
	//Вывести на форму Команду РассчитатьСкидку
	Команда = Команды.Добавить("РассчитатьСкидку");    
	Команда.Заголовок = "Рассчитать скидку";
	Команда.Действие = "Д_РассчитатьСкидкуДействие"; //добавлена в конце 
	
	ЭлементКнопка = Элементы.Добавить("КнопкаРассчитатьСкидку", Тип("КнопкаФормы"), ГруппаСогласованнаяСкидка);
	ЭлементКнопка.ИмяКоманды = "РассчитатьСкидку";
	ЭлементКнопка.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
    //--Грунтова Задание№13 17.09.2024
    
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
    ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РассчитатьСуммуСтроки(ТекущиеДанные)
	
	КоэффициентСкидки = 1 - ТекущиеДанные.Скидка / 100;
	ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество * КоэффициентСкидки;
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДокумента()
	
	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма") + Объект.Услуги.Итог("Сумма");
	
КонецПроцедуры

&НаКлиенте
Процедура Д_РассчитатьСкидкуДействие(Команда)
	//++Грунтова Задание№13 17.09.2024 
	 Д_РассчитатьСтрокиСоСкидкой();
	 
	 РассчитатьСуммуДокумента(); 
	 //--Грунтова Задание№13 17.09.2024
КонецПроцедуры

&НаКлиенте
Процедура Д_РассчитатьСтрокиСоСкидкой()
	//++Грунтова Задание№13 17.09.2024
	Для Каждого ТекущиеДанные Из Объект.Товары Цикл
		Скидка = Окр(ТекущиеДанные.Цена * ТекущиеДанные.Количество * Объект.КЛ_СогласованнаяСкидка /100, 2);
		СкидкаТЧ = Окр(ТекущиеДанные.Цена * ТекущиеДанные.Количество * ТекущиеДанные.Скидка /100, 2);
		
		ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество - (СкидкаТЧ + Скидка); 
	КонецЦикла;  
	
	Для Каждого ТекущиеДанные Из Объект.Услуги Цикл
		Скидка = Окр(ТекущиеДанные.Цена * ТекущиеДанные.Количество * Объект.КЛ_СогласованнаяСкидка /100, 2);
		СкидкаТЧ = Окр(ТекущиеДанные.Цена * ТекущиеДанные.Количество * ТекущиеДанные.Скидка /100, 2);
		
		ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество - (СкидкаТЧ + Скидка);
	КонецЦикла;
    //--Грунтова Задание№13 17.09.2024
КонецПроцедуры

&НаКлиенте
Асинх Процедура ВыполнитьДействиеСогласованнаяСкидкаПриИзменении(Элемент)
	//++Грунтова Задание№13 17.09.2024
	Ответ = ВопросАсинх("Вы уверены, что хотите применить скидку?", РежимДиалогаВопрос.ДаНет);
	
	Результат = Ждать Ответ;
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	ИначеЕсли Результат = КодВозвратаДиалога.Да Тогда
		Д_РассчитатьСтрокиСоСкидкой();
	КонецЕсли;
	//--Грунтова Задание№13 17.09.2024
КонецПроцедуры

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти
