
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	//+netology Вставка
	ДобавитьПоляДляРасчетаСкидки()
	//-netology КонецВставки
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
		
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
    ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РассчитатьСуммуСтроки(ТекущиеДанные)
	
	//+netology Удаление
	//ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество;
	//-netology КонецУдаления
	
	//+netology Вставка
	ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество * (1 - Объект.netology_ПроцентСкидки/100);
	//-netology КонецВставки
	
	РассчитатьСуммуДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДокумента()
	
	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма") + Объект.Услуги.Итог("Сумма");
	
КонецПроцедуры

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти

#Область netology

&НаСервере
Процедура ДобавитьПоляДляРасчетаСкидки()
	
	Группа = Элементы.Добавить("ГруппаОбычная", Тип("ГруппаФормы"),Элементы.ГруппаШапкаЛево);
	Группа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	Группа.Отображение = ОтображениеОбычнойГруппы.Нет;
	Группа.ОтображатьЗаголовок = ЛОЖЬ; 
	Группа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;     
	
	ПолеВвода = Элементы.Добавить(
		"netology_ПроцентСкидки",    
		Тип("ПолеФормы"), 
		Группа);	      
	ПолеВвода.Заголовок = "Процент скидки";
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.netology_ПроцентСкидки"; 
	ПолеВвода.УстановитьДействие("ПриИзменении","ПриИзмененииПроцентСкидки"); 
	
	//Добавление команды формы
	Команда = Команды.Добавить(
		"КомандаПересчитатьТаблицу"); 
	Команда.Заголовок = "Пересчитать таблицу";
	Команда.Действие  = "НажатиеКнопкиПересчитатьТаблицу"; 
	Команда.Картинка = БиблиотекаКартинок.Обновить;
	Команда.Отображение = ОтображениеКнопки.КартинкаИТекст;
	
	//Добавление кнопки формы
	КнопкаФормы = Элементы.Добавить(
		"КнопкаПересчитатьТаблицу", 
		Тип("КнопкаФормы"), 
		Группа); 
	КнопкаФормы.ИмяКоманды = "КомандаПересчитатьТаблицу"; 
	КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка;   
	
	
КонецПроцедуры	

&НаКлиенте
Процедура НажатиеКнопкиПересчитатьТаблицу()

	ПересчитатьСуммыСтрок();
	
КонецПроцедуры  

&НаКлиенте
Асинх Процедура ПриИзмененииПроцентСкидки()
	
	Если Объект.Товары.Количество() = 0 и Объект.Услуги.Количество() = 0 тогда
		возврат;
	КонецЕсли;
	
	Ответ = Ждать ВопросАсинх(НСтр("ru = 'Изменен процент скидки. 
	|Пересчитать таблицы товаров и услуг?'"), РежимДиалогаВопрос.ДаНет);
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда 
		Возврат;
    КонецЕсли;
	
    ПересчитатьСуммыСтрок();
	
КонецПроцедуры   

&НаКлиенте
Процедура ПересчитатьСуммыСтрок()
	
	Для Каждого стр из Объект.Товары цикл
		РассчитатьСуммуСтроки(стр);			
	КонецЦикла;    
	
	Для Каждого стр из Объект.Услуги цикл
		РассчитатьСуммуСтроки(стр);			
	КонецЦикла; 
		
КонецПроцедуры

#КонецОбласти

