#Область ПрограммныйИнтерфейс

// Дополнить форму элементами.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
Процедура ДополнитьФормуЭлементами(Форма) Экспорт
	
	ИмяФормы = Форма.ИмяФормы;
	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
		ДобавитьПолеСкидкаИКомандуПересчет(Форма);
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма)
	
	ПолеВвода = Форма.Элементы.Добавить("ДМ_КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ДМ_КонтактноеЛицо";

КонецПроцедуры

Процедура ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма)
	
	ПолеВвода = Форма.Элементы.Вставить("ДМ_КонтактноеЛицо", Тип("ПолеФормы"), , Форма.Элементы.СуммаДокумента);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ДМ_КонтактноеЛицо";

КонецПроцедуры

Процедура ДобавитьПолеСкидкаИКомандуПересчет(Форма)
	
	ГруппаСкидкаИПересчет = Форма.Элементы.Добавить("ДМ_ГруппаСкидкаИПересчет", Тип("ГруппаФормы"), Форма.Элементы.ГруппаШапкаЛево);
	ГруппаСкидкаИПересчет.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаСкидкаИПересчет.Заголовок = "Скидка"; 
	ГруппаСкидкаИПересчет.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаСкидкаИПересчет.ОтображатьЗаголовок = Ложь;
	ГруппаСкидкаИПересчет.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	
	ПолеВводаСкидки = Форма.Элементы.Добавить("ДМ_СогласованнаяСкидка", Тип("ПолеФормы"), Форма.Элементы.ДМ_ГруппаСкидкаИПересчет);
	ПолеВводаСкидки.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВводаСкидки.ПутьКДанным = "Объект.ДМ_СогласованнаяСкидка";
	ПолеВводаСкидки.УстановитьДействие("ПриИзменении", "Подключаемый_ДМ_СогласованнаяСкидкаПриИзменении");
	
	КомандаПересчет = Форма.Команды.Добавить("ДМ_ПересчитатьТаблицу");
	КомандаПересчет.Заголовок = "Пересчитать таблицу";
	КомандаПересчет.Действие = "ДМ_ПересчитатьТаблицу";
	
	КнопкаФормы = Форма.Элементы.Добавить("КнопкаПересчитатьТаблицу", Тип("КнопкаФормы"), Форма.Элементы.ДМ_ГруппаСкидкаИПересчет);
	КнопкаФормы.ИмяКоманды = "ДМ_ПересчитатьТаблицу";
	КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	КнопкаФормы.Картинка = БиблиотекаКартинок.Обновить;
	КнопкаФормы.Отображение = ОтображениеКнопки.КартинкаИТекст;

КонецПроцедуры


#КонецОбласти
