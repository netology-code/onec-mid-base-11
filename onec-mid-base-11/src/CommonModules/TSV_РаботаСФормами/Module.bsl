 
 Процедура ДополнитьФорму(Форма) Экспорт 
	
	ИмяФормы = Форма.ИмяФормы;
	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
		ДобавитьГруппаСкидкаКнопкаНаФорму(Форма);
		ДобавитьПолеСогласованнаяСкидкаВГруппаСкидкаКнопка(Форма);
		ДобавитьКнопкуПересчитатьТаблицуВГруппаСкидкаКнопка(Форма);		
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);		
	ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма); 	        
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоНаФорму(Форма);		
	ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоНаФорму(Форма); 
    КонецЕсли;
     	 
КонецПроцедуры  


Процедура ДобавитьГруппаСкидкаКнопкаНаФорму(Форма)
	
	ГруппаСкидкаКнопка = Форма.Элементы.Добавить("ГруппаСкидкаКнопка", Тип("ГруппаФормы")
		,Форма.Элементы.ГруппаШапкаЛево);
	ГруппаСкидкаКнопка.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаСкидкаКнопка.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаСкидкаКнопка.ОтображатьЗаголовок = ЛОЖЬ;
	ГруппаСкидкаКнопка.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;

КонецПроцедуры


Процедура ДобавитьКнопкуПересчитатьТаблицуВГруппаСкидкаКнопка(Форма)Экспорт
	
	ИмяКоманды = "ПересчитатьТаблицу";
	ДобавляемаяКоманда = Форма.Команды.Добавить(ИмяКоманды);
	ДобавляемаяКоманда.Действие = "ПересчитатьТаблицу";
	НоваяКнопкаФормы = Форма.Элементы.Вставить(ИмяКоманды, Тип("КнопкаФормы")
		,Форма.Элементы.ГруппаСкидкаКнопка);
	НоваяКнопкаФормы.ИмяКоманды = ИмяКоманды;
	НоваяКнопкаФормы.Доступность = Истина;
КонецПроцедуры  



Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма)
		       		 	
	ПолеВвода = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.КонтактноеЛицо";
		    		
КонецПроцедуры


    
Процедура ДобавитьПолеКонтактноеЛицоНаФорму(Форма)
			        
	ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы")
		,,Форма.Элементы.СуммаДокумента);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.КонтактноеЛицо"; 
		
КонецПроцедуры 



Процедура ДобавитьПолеСогласованнаяСкидкаВГруппаСкидкаКнопка(Форма)Экспорт
	       		 	
	ПолеВвода = Форма.Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы")
		, Форма.Элементы.ГруппаСкидкаКнопка);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.СогласованнаяСкидка";
    ПолеВвода.УстановитьДействие("ПриИзменении","ПриИзмененииСогласованнаяСкидка");		
КонецПроцедуры






