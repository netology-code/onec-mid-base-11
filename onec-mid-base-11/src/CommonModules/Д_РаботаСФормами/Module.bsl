#Область ПрограммныйИнтерфейс

// Дополнить форму.
// Программно выводит поле на форму
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
Процедура ДополнитьФорму(Форма) Экспорт

	ИмяФормы = Форма.ИмяФормы;
	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		 ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
		 ДобавитьПолеСогласованнаяСкидкаИКомандуПересчитатьТаблицуВГруппаШапкаЛево(Форма); 
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
         ДобавитьПолеКонтактноеЛицоПередСуммаДокумента(Форма); 
	ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
         ДобавитьПолеКонтактноеЛицоПередСуммаДокумента(Форма);
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
         ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
         ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	КонецЕсли;
	 
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции  

Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма)
	
	Элемент = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво); 
	Элемент.Вид = ВидПоляФормы.ПолеВвода;
	Элемент.ПутьКДанным = "Объект.Д_КонтактноеЛицо";
	
КонецПроцедуры 

Процедура ДобавитьПолеКонтактноеЛицоПередСуммаДокумента(Форма)
	
	Элемент = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), , Форма.Элементы.СуммаДокумента); 
	Элемент.Вид = ВидПоляФормы.ПолеВвода;
	Элемент.ПутьКДанным = "Объект.Д_КонтактноеЛицо";
	
КонецПроцедуры       

Процедура ДобавитьПолеСогласованнаяСкидкаИКомандуПересчитатьТаблицуВГруппаШапкаЛево(Форма)
	
	Группа = Форма.Элементы.Вставить("ГруппаСкидка", Тип("ГруппаФормы"), Форма.Элементы.ГруппаШапкаЛево); 
	Группа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	Группа.Заголовок = "";
	
	Элемент = Форма.Элементы.Вставить("СогласованнаяСкидка", Тип("ПолеФормы"), Группа);
	Элемент.Вид = ВидПоляФормы.ПолеВвода;
	Элемент.ПутьКДанным = "Объект.Д_СогласованнаяСкидка";
	Элемент.УстановитьДействие("ПриИзменении", "Д_СогласованнаяСкидкаПриИзмененииВместо");    
	
	Команда = Форма.Команды.Добавить("ПересчитатьТаблицу");     
	Команда.Заголовок = "Пересчитать таблицу";
	Команда.Действие = "Д_КомандаПересчитатьТаблицу";
	
	КнопкаФормы = Форма.Элементы.Добавить("КнопкаПересчитатьТаблицу", Тип("КнопкаФормы"), Группа);
	КнопкаФормы.ИмяКоманды = "ПересчитатьТаблицу"; 
	КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка;     
	
КонецПроцедуры   

#КонецОбласти