#Область СлужебныйПрограммныйИнтерфейс

Процедура ОтрисоватьДополнительныеЭлементы(Форма) Экспорт
     
     ИмяФормы = Форма.ИмяФормы;
     
     Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
         ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
         ДобавитьПолеИКнопкуДляРассчетаСкидки(Форма);
         
     ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
         ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
         
     ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
         ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
         
     ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
         ДобавитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);
         
     ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
         ДобавитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);
         
     КонецЕсли; 
     
КонецПроцедуры 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма)
    
    ПолеКонтактноеЛицо = Форма.Элементы.Добавить("АВ_КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
    ПолеКонтактноеЛицо.Вид = ВидПоляФормы.ПолеВвода;
    ПолеКонтактноеЛицо.ПутьКДанным = "Объект.АВ_КонтактноеЛицо";
    
КонецПроцедуры


Процедура ДобавитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма)
    
    ПолеКонтактноеЛицо = Форма.Элементы.Вставить("АВ_КонтактноеЛицо", Тип("ПолеФормы"), , Форма.Элементы.СуммаДокумента);
    ПолеКонтактноеЛицо.Вид = ВидПоляФормы.ПолеВвода;
    ПолеКонтактноеЛицо.ПутьКДанным = "Объект.АВ_КонтактноеЛицо"; 
    
КонецПроцедуры


Процедура ДобавитьПолеИКнопкуДляРассчетаСкидки(Форма)
    
    ГруппаСкидка = Форма.Элементы.Добавить("ГруппаСкидка", Тип("ГруппаФормы"),Форма.Элементы.ГруппаШапкаЛево);
    ГруппаСкидка.Вид = ВидГруппыФормы.ОбычнаяГруппа; 
    ГруппаСкидка.ОтображатьЗаголовок = Ложь;
    ГруппаСкидка.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
    
    ПолеСогласованнаяСкидка = Форма.Элементы.Добавить("АВ_СогласованнаяСкидка", Тип("ПолеФормы"), Форма.Элементы.ГруппаСкидка);
    ПолеСогласованнаяСкидка.Вид = ВидПоляФормы.ПолеВвода;
    ПолеСогласованнаяСкидка.ПутьКДанным = "Объект.АВ_СогласованнаяСкидка";
    ПолеСогласованнаяСкидка.УстановитьДействие ("ПриИзменении", "АВ_СогласованнаяСкидкаПриИзменении");
    
    Команда = Форма.Команды.Добавить("ПересчитатьТаблицу");
    Команда.Заголовок = "Пересчитать таблицу";
    Команда.Действие = "АВ_ПересчитатьТаблицуПосле";
    Команда.Отображение = ОтображениеКнопки.КартинкаИТекст;
    Команда.Картинка = БиблиотекаКартинок.Обновить;
        
    КнопкаФормы = Форма.Элементы.Добавить("КнопкаПересчитатьТаблицу", Тип("КнопкаФормы"), Форма.Элементы.ГруппаСкидка);
    КнопкаФормы.ИмяКоманды = "ПересчитатьТаблицу";
    КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка; 
    
КонецПроцедуры 

#КонецОбласти