  #Область ПрограммныйИнтерфейс

Процедура ДополнитьФорму(Форма) Экспорт 
	
	ИмяФормы = Форма.ИмяФормы;
	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);	
		ДобавитьПоляДляРасчетаСкидки(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" тогда
		ДобавитьПолеКонтактноеЛицоПередСуммаДокумента(Форма);	
	ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" тогда
		ДобавитьПолеКонтактноеЛицоПередСуммаДокумента(Форма);	
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);	
	ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);	
    КонецЕсли;
		
	
КонецПроцедуры
  
  #КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма)
	
	ПолеВвода = Форма.Элементы.Добавить(
		"netology_КонтактноеЛицо",    
		Тип("ПолеФормы"), 
		Форма.Элементы.ГруппаШапкаПраво);	      
	ПолеВвода.Заголовок = "Контактное лицо";
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.netology_КонтактноеЛицо";
	
КонецПроцедуры 

Процедура ДобавитьПолеКонтактноеЛицоПередСуммаДокумента(Форма)

	ПолеВвода = Форма.Элементы.Вставить(
		"netology_КонтактноеЛицо",    
		Тип("ПолеФормы"),, 
		Форма.Элементы.СуммаДокумента);	      
	ПолеВвода.Заголовок = "Контактное лицо";
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.netology_КонтактноеЛицо";
	
КонецПроцедуры   

Процедура ДобавитьПоляДляРасчетаСкидки(Форма)
	
	Группа = Форма.Элементы.Добавить("ГруппаОбычная", Тип("ГруппаФормы"),Форма.Элементы.ГруппаШапкаЛево);
	Группа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	Группа.Отображение = ОтображениеОбычнойГруппы.Нет;
	Группа.ОтображатьЗаголовок = ЛОЖЬ; 
	Группа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;     
	
	ПолеВвода = Форма.Элементы.Добавить(
		"netology_ПроцентСкидки",    
		Тип("ПолеФормы"), 
		Группа);	      
	ПолеВвода.Заголовок = "Процент скидки";
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.netology_ПроцентСкидки"; 
	ПолеВвода.УстановитьДействие("ПриИзменении","ПриИзмененииПроцентСкидки"); 
	
	//Добавление команды формы
	Команда = Форма.Команды.Добавить(
		"КомандаПересчитатьТаблицу"); 
	Команда.Заголовок = "Пересчитать таблицу";
	Команда.Действие  = "НажатиеКнопкиПересчитатьТаблицу"; 
	Команда.Картинка = БиблиотекаКартинок.Обновить;
	Команда.Отображение = ОтображениеКнопки.КартинкаИТекст;
	
	//Добавление кнопки формы
	КнопкаФормы = Форма.Элементы.Добавить(
		"КнопкаПересчитатьТаблицу", 
		Тип("КнопкаФормы"), 
		Группа); 
	КнопкаФормы.ИмяКоманды = "КомандаПересчитатьТаблицу"; 
	КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка;   
	
КонецПроцедуры 



#КонецОбласти
